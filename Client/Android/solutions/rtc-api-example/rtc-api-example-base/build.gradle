plugins {
    id 'com.android.library'
}

android {
    compileSdk project.compileSdkVersion

    defaultConfig {
        namespace 'com.vertc.api.example.base'

        minSdkVersion project.minSdkVersion
        targetSdkVersion project.targetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField('String', 'APP_ID', "\"${safeGetProp('APP_ID')}\"")
        buildConfigField('String', 'APP_KEY', "\"${safeGetProp('APP_KEY')}\"")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        buildConfig true
    }
}

dependencies {
    implementation libs.appcompat
    implementation libs.constraintlayout
    implementation libs.recyclerview

    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core
}

def safeGetProp(prop, defaultValue = '') {
    if (rootProject.hasProperty(prop)) {
        return rootProject.property(prop)
    } else if (project.hasProperty(prop)) {
        return project.property(prop)
    } else {
        return defaultValue
    }
}