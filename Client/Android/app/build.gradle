plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
    compileSdk project.compileSdkVersion

    defaultConfig {
        applicationId "com.byteplus.videoone.android"
        minSdkVersion project.minSdkVersion
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion project.targetSdkVersion

        versionCode project.APP_VERSION_CODE.toInteger()
        versionName project.APP_VERSION_NAME

        ndk { abiFilters 'arm64-v8a', 'armeabi-v7a' }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        multiDexEnabled true

        buildConfigField('String', 'TERMS_OF_SERVICE_URL', "\"$TERMS_OF_SERVICE_URL\"")
        buildConfigField('String', 'PRIVACY_POLICY_URL', "\"$PRIVACY_POLICY_URL\"")
        buildConfigField('String', 'GITHUB_REPO', "\"$GITHUB_REPO\"")

        manifestPlaceholders = [appName: project.APP_NAME]

        setProperty("archivesBaseName", "VideoOne-$APP_VERSION_NAME")
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    packagingOptions {
        pickFirst 'lib/*/libc++_shared.so'
    }
}

dependencies {
    implementation(platform("org.jetbrains.kotlin:kotlin-bom:1.9.22"))

    implementation libs.appcompat
    implementation libs.recyclerview
    implementation libs.constraintlayout
    implementation libs.material

    implementation libs.navigation.fragment.ktx
    implementation libs.navigation.ui.ktx

    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core

    implementation libs.lifecycle.runtime.ktx

    implementation libs.eventbus

    implementation libs.glide
    kapt libs.glide.compiler

    implementation project(':component:solution-base')
    implementation project(':component:loginkit')
    implementation project(':component:rtc-toolkit')
    implementation project(':component:avatars')

    // region Solutions
    implementation project(':solutions:interactivelive')
    implementation project(':solutions:ktv')
    implementation project(':solutions:vod:vod-demo')
    implementation project(':solutions:media-live:live-entrance')
    implementation project(':solutions:rtc-api-example:rtc-api-example-entry')
    // endregion
}
