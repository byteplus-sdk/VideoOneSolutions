plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
    namespace 'com.vertcdemo.app'
    compileSdk project.compileSdkVersion

    defaultConfig {
        applicationId "com.byteplus.videoone.android"
        minSdkVersion project.minSdkVersion
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion project.targetSdkVersion
        compileSdk project.compileSdkVersion

        versionCode project.APP_VERSION_CODE.toInteger()
        versionName project.APP_VERSION_NAME

        ndk { abiFilters 'arm64-v8a', 'armeabi-v7a' }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        multiDexEnabled true

        buildConfigField('String', 'TERMS_OF_SERVICE_URL', "\"$TERMS_OF_SERVICE_URL\"")
        buildConfigField('String', 'PRIVACY_POLICY_URL', "\"$PRIVACY_POLICY_URL\"")
        buildConfigField('String', 'GITHUB_REPO', "\"$GITHUB_REPO\"")

        manifestPlaceholders = [appName: project.APP_NAME]

        setProperty("archivesBaseName", "VideoOne-$APP_VERSION_NAME-$APP_VERSION_CODE-${new Date().format("yyyyMMddHHmm")}")
    }

    buildFeatures {
        buildConfig true
        viewBinding true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(project.javaLanguageVersion)
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    packagingOptions {
        pickFirst 'lib/*/libc++_shared.so'
    }

    buildFeatures {
        buildConfig true
    }
}

dependencies {
    implementation(platform("org.jetbrains.kotlin:kotlin-bom:1.9.22"))

    implementation libs.appcompat
    implementation libs.activity
    implementation libs.recyclerview
    implementation libs.constraintlayout
    implementation libs.material

    implementation libs.splashscreen

    implementation libs.bundles.navigation

    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core

    implementation libs.eventbus

    implementation libs.glide
    kapt libs.glide.compiler

    implementation project(':component:solution-base')
    implementation project(':component:loginkit')
    implementation project(':component:rtc-toolkit')
    implementation project(':component:avatars')

    // region Solutions
    implementation project(':solutions:interactivelive')
    implementation project(':solutions:ktv')
    implementation project(':solutions:vod:vod-demo')
    implementation project(':solutions:vod:vod-function')
    implementation project(':solutions:media-live:live-entrance')
    implementation project(':solutions:rtc-api-example:rtc-api-example-entry')
    implementation project(':solutions:vod-live-feed')
    implementation project(':solutions:vod-mini-drama')
    // endregion
}
