plugins {
    id 'com.android.library'
}

android {
    compileSdkVersion project.compileSdkVersion

    defaultConfig {
        minSdkVersion project.minSdkVersion
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion project.targetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        ensureProperty('SERVER_URL')
        buildConfigField('String', 'SERVER_URL', "\"${project.SERVER_URL}\"")

        buildConfigField('String', 'APP_ID', "\"${project.APP_ID}\"")
        buildConfigField('String', 'APP_KEY', "\"${project.APP_KEY}\"")
        buildConfigField('String', 'ACCESS_KEY_ID', "\"${project.ACCESS_KEY_ID}\"")
        buildConfigField('String', 'SECRET_ACCESS_KEY', "\"${project.SECRET_ACCESS_KEY}\"")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation "androidx.appcompat:appcompat:$AppCompatVersion"
    implementation "com.google.android.material:material:$MaterialVersion"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    api (CORE_SDK) {
        exclude group: 'com.bytedance.applog', module: 'RangersAppLog-Lite-global'
    }

    implementation "org.greenrobot:eventbus:$EventBusVersion"
    implementation "com.google.code.gson:gson:$GsonVersion"
    implementation "com.squareup.okhttp3:okhttp:$OkHttpVersion"
}

private def ensureProperty(String propertyName) {
    if (!project.hasProperty(propertyName) || project[propertyName].isEmpty()) {
        throw new GradleException("NO ${propertyName} defined in gradle.properties!")
    }
}